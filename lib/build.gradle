apply plugin: 'org.jetbrains.dokka'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven'

ext.projectName = 'JRAW'

dependencies {
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"

    implementation "com.squareup.moshi:moshi:$moshiVersion"
    testImplementation("com.squareup.moshi:moshi-kotlin:$moshiVersion") {
        exclude group: 'com.squareup.moshi'
    }

    kapt('com.ryanharter.auto.value:auto-value-moshi:0.4.5') {
        exclude group: 'com.squareup.moshi'
    }
    kapt "com.google.auto.value:auto-value:$autoValueVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"

    compileOnly "com.google.auto.value:auto-value:$autoValueVersion"
    compileOnly('com.ryanharter.auto.value:auto-value-moshi-annotations:0.4.5') {
        exclude group: 'com.squareup.moshi'
    }

    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttpVersion"
    testImplementation 'net.sourceforge.htmlunit:htmlunit:2.29'
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    testImplementation 'org.reflections:reflections:0.9.10'
}

sourceSets {
    gen // Generated Java code (see the :meta project)
    main {
        java {
            // Include generated sources
            source gen.allSource
        }
    }
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from dokkaJavadoc.outputDirectory
    baseName = projectName
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    baseName = projectName
}

jar {
    baseName = projectName
}

// When JitPack builds, it runs the install task (from the maven plugin) to install all artifacts listed here to the
// local repository. List the sources and Javadoc jar here to make it available to JitPack users. The bintrayUpload task
// also uses this to find which jars to upload on release.
artifacts {
    archives sourcesJar
    archives javadocJar
}
